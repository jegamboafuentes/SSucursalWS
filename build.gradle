apply {
    plugin 'java'
    plugin 'maven'
    plugin 'eclipse-wtp'
    plugin 'sonar-runner'
    plugin 'jetty'
    plugin 'war'
}

group = projectGroup
archivesBaseName = projectArchivesBaseName
version = projectVersion

description = projectDescription
sourceCompatibility = projectSourceCompatibility
targetCompatibility = projectTargetCompatibility

[compileJava, compileTestJava]*.options*.encoding = projectEncoding

[jettyRun, jettyRunWar, jettyStop]*.stopPort = jettyPort.asType(Integer.class)
[jettyRun, jettyRunWar, jettyStop]*.stopKey = 'stopKey'

ext {       
    archivaUploadCredential = {
        authentication(
            userName: archivaUser, 
            password: archivaPassword)
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", "http://localhost:18090/sonarqube"
    }
}

repositories {
    maven {
        url internalRepository
        
        credentials {
            username archivaUser
            password archivaPassword
        }
    }
    
    maven {
        url libsRepository
        
        credentials {
            username archivaUser
            password archivaPassword
        }
    }
    
    maven {
        url releasesRepository
        
        credentials {
            username archivaUser
            password archivaPassword
        }
    }
    
    maven {
        url snapshotRepository
        
        credentials {
            username archivaUser
            password archivaPassword
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(
                url: releasesRepository, project.ext.archivaUploadCredential)        
            snapshotRepository(
                url: snapshotRepository, project.ext.archivaUploadCredential)
        }
    }
}

dependencies {
    providedCompile "javax.servlet:servlet-api:$javaxServletVersion",
                    "javax.ws.rs:jsr311-api:$jsr311Version"
    
    compile "aopalliance:aopalliance:$aopallianceVersion",
            "commons-logging:commons-logging:$commonsLoggingVersion",
    
            "org.springframework:spring-aop:$springVersion",
            "org.springframework:spring-beans:$springVersion",
            "org.springframework:spring-context:$springVersion",
            "org.springframework:spring-core:$springVersion",
            "org.springframework:spring-expression:$springVersion",
            "org.springframework:spring-web:$springVersion",
            "org.springframework:spring-webmvc:$springVersion",
    
            "org.springframework:spring-jdbc:$springVersion",
            "org.springframework:spring-tx:$springVersion",    
                        
            "org.jvnet.jax-ws-commons.spring:jaxws-spring:$jaxwsSpringVersion",
    
            "com.fasterxml.jackson.core:jackson-core:$jacksonVersion",
            "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion",
            "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",                   
         
            "com.baz.scc.apisucursal:cjcrl-apisucursal:$apiSucusalVersion" 
          
    
    runtime "javax.servlet:jstl:$jstlVersion",
    
            "com.sun.jersey:jersey-core:$jerseyVersion",
            "com.sun.jersey:jersey-client:$jerseyVersion",
            "com.sun.jersey:jersey-json:$jerseyVersion",
            
            "org.codehaus.jettison:jettison:$jettisonVersion",
            "com.sun.xml.ws:jaxws-rt:$jaxwsrtVersion"
            
            
    
    testCompile "junit:junit:$junitVersion",
                "org.springframework:spring-test:$springVersion",
                "org.hamcrest:hamcrest-core:$hamcrestCoreVersion"
}

jar {   
    manifest {
        attributes 'Implementation-Title': projectTitle, 
                   'Implementation-Version': projectVersion, 
                   'Class-Path': configurations.runtime.files.collect { "libs/$it.name" }.join(' ')
    }
}

javadoc {
    options.encoding = projectEncoding
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    classifier = 'javadoc'
    
    from javadoc.destinationDir    
}

task sourcesJar(type: Jar) {
    classifier = 'sources'

    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

test {
    testLogging {
        events 'failed'
        exceptionFormat 'short'
        
        debug {
            events 'started', 'skipped', 'failed'
            exceptionFormat 'full'
        }
        
        error {
            events 'skipped', 'failed'
            exceptionFormat 'full'
        }
    }
}
